/****************************************************************************\

    SCToken.SI

    Routines to handle quoted filenames

    Version         v2.00/24.10.96
    Copyright       (c) 1995-96 by DiK

    History

    v2.00/24.10.96  first version

\****************************************************************************/

/****************************************************************************\
    add quotes to filname (16-bit version dummy)
\****************************************************************************/

#ifndef WIN32
#ifdef  INC_QUOTE

string proc QuotePath( string name )
    return(name)
end

#endif
#endif

/****************************************************************************\
    remove quotes from filename
\****************************************************************************/

#ifdef  INC_UNQUOTE

string proc UnQuotePath( string name )
    if name[1] == '"' and name[Length(name)] == '"'
        return(name[2..Length(name)-1])
    endif
    return(name)
end

#endif

/****************************************************************************\
    parse command line including quotes
\****************************************************************************/

#ifdef  INC_NUMTOKEN

#ifndef INC_SPLITTOKEN
#define INC_SPLITTOKEN 1
forward proc SplitQuotedString( var string head, var string tail, string name )
#endif

integer proc NumQuotedTokens( string name )
    integer count = 0
    string head[255] = ''
    string tail[255] = Trim(name)

    while Length(tail)
        count = count + 1
        SplitQuotedString(head,tail,tail)
    endwhile
    return(count)
end

#endif

#ifdef  INC_GETTOKEN

#ifndef INC_SPLITTOKEN
#define INC_SPLITTOKEN 1
forward proc SplitQuotedString( var string head, var string tail, string name )
#endif

string proc GetQuotedToken( string name, integer index )
    string head[255] = ''
    string tail[255] = Trim(name)

    while index > 0
        SplitQuotedString(head,tail,tail)
        index = index - 1
    endwhile
    return(head)
end

#endif

#ifdef  INC_SPLITTOKEN

proc SplitQuotedString( var string head, var string tail, string name )
    integer p

    if Length(name) == 0
        head = ''
        tail = ''
    else
        tail = name
        if tail[1] == '"'
            tail = tail[2:Length(tail)]
            p = Pos('"',tail)
        else
            p = Pos(' ',tail)
        endif
        if p == 0
            p = Length(tail) + 1
        endif
        head = tail[1..p-1]
        tail = LTrim(tail[p+1:Length(tail)])
    endif
end

#endif

