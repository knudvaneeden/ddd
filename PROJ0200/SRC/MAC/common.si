//#pragma inhouse,chrisant

// COMMON.SI --------------------------------------------------------------
// include file for PROJ macro and PJFILE macro



#ifdef NEED_MSEARCHPATH
#ifndef NEED_EXPANDENVVARS
#define NEED_EXPANDENVVARS 1
#endif
#endif



///////////////////////////////////////////////////////////////////////////
// Constants

// version is automatically updated in the help file!
string c_stVer[] = "v2.00"
constant c_nVer = 0x0200

string c_stXTALK[] = "PROJ_MacroTalk"
string c_stOpenPath[] = "PROJ_OpenPath"


#ifdef NEED_SETPROJECTROOT
string c_stProjectRoot[] = "PJROOT"		// 'root' of the current project
#endif


constant MAXPATH = _MAXPATH_
constant MAXSTR = 255
constant MAXEXT = 12
constant MAXTITLE = 48


constant DEF_TIMEOUT = 2
constant ERROR_TIMEOUT = 5


constant ESC = 0
constant OK = 1
constant YES = 1
constant NO = 2
constant CANCEL = 3


constant GREY_KEY = 0x8000
constant SHIFT_KEY = 0x0100


string c_stTitle[] = "Proj"

string c_stExt[] = ".pj"
string c_stLocalProject[] = "local.pj"

string c_stIniSection[] = "Proj"
#ifdef NEED_INIVERSION
string c_stIniVersion[] = "Version"
#endif
#ifdef NEED_WRITEPROFILE
string c_stIniLastProject[] = "LastProject"
#endif

string c_stProjDir[] = "ProjDir"

constant chSubdirs = Asc(">")
constant chExclude = Asc("<")

string c_stDiv[] = Chr(1)


forward proc TimeOutMessage(integer cSeconds, string msg)

#ifdef NEED_MF
forward integer proc mfOpen(integer ul)
forward integer proc mfNotPrompt(integer ul)
#endif

#ifdef NEED_EXPANDENVVARS
forward integer proc ExpandEnvVars(var string st)
#endif



///////////////////////////////////////////////////////////////////////////
// Earlier than TSE 3.0

#ifndef TSE30
integer proc EquiStr(string s1, string s2)
	return(Lower(s1) == Lower(s2))
end

integer proc NumFileTokens(string s)
	return(NumTokens(s, ' "'))
end

string proc GetFileToken(string s, integer n)
	return(GetToken(s, ' "', n))
end
#endif

#ifdef NEED_ISWHITESTR
integer proc isWhiteStr(string s)
	integer i

	if not Length(s)
		return(FALSE)
	endif

	for i = 1 to Length(s)
		if not (s[i] in Chr(32), Chr(9))
			return(FALSE)
		endif
	endfor

	return(TRUE)
end
#endif



///////////////////////////////////////////////////////////////////////////
// Gui Oem Support

#ifdef WIN32
#ifdef EDITOR_VERSION
#if EDITOR_VERSION > 0x3000
#define GUI_AWARE_COMPILER 1
#endif
#endif
#endif // WIN32


#ifdef GUI_AWARE_COMPILER

proc mPutOemStrXY(integer x, integer y, string st, integer attr, integer f3D)
	PutOemStrXY(x, y, st, attr, iif(f3D, 1, 0))
end

proc mPutOemStr(string st, integer attr, integer f3D)
	mPutOemStrXY(VWhereX(), VWhereY(), st, attr, f3D)
end

#if 0
integer proc FUse3D()
	constant USE_3D_CHARS = 0x0002	// _USE_3D_CHARS_ from TSE 4.0
	return(Query(SpecialEffects) & USE_3D_CHARS)
end
#endif

#else // !GUI_AWARE_COMPILER

proc mPutOemStrXY(integer x, integer y, string st, integer attr, integer f3D)
	PutStrXY(x, y, st, attr)
end

proc mPutOemStr(string st, integer attr, integer f3D)
	PutStr(st, attr)
end

#if 0
integer proc FUse3D()
	return(FALSE)
end
#endif

proc PutOemCharV(string s, integer len)
	PutCharV(s, len)
end

proc PutOemChar(string s)
	PutChar(s)
end

#endif // !GUI_AWARE_COMPILER



///////////////////////////////////////////////////////////////////////////
// Functions

#ifdef DEBUG
proc Assert(integer f, string st)
	if not f
		Warn(st)
	endif
end
#endif


#ifdef NEED_BEEP
proc Beep()
	//$ todo: have a "silent" option that disables this
	Alarm()
end
#endif


proc Hilite()
	//HiliteFoundText()
end


string proc OnOffStr(integer f)
	return(iif(f, "On", "Off"))
end


#ifdef NEED_TOGGLEVAR
integer proc ToggleVar(var integer f)
	f = not f
	return(f)
end
#endif


proc Toggle_Int(integer iIntVar)
	Set_Int(iIntVar, not Get_Int(iIntVar))
end


#ifdef NEED_ELLIPSIFY
string proc Ellipsify(string s, integer len)
	if Length(s) > len
		return (SubStr(s, 1, len-3)+"...")
	endif
	return (s)
end
#endif


#ifdef NEED_GETTEMPFILE
string proc GetTempFile()
	string dir[_MAXPATH_]

	dir = GetEnvStr("TEMP")
	if dir == ""
		dir = GetEnvStr("TMP")
		if dir == ""
			dir = CurrDir()
		endif
	endif
	return (MakeTempName(dir))
end
#endif


/*
integer proc LongestLineInBlock()
	integer c = 0

	if isBlockInCurrFile()
		PushPosition()
		GotoBlockBegin()
		repeat
			c = max(c, CurrLineLen())
		until not Down() or not isCursorInBlock()
		PopPosition()
	endif
	return(c)
end
*/


#ifdef NEED_GETHELPLINEROW
integer proc GetHelpLineRow()
	return(iif(Query(StatusLineAtTop), Query(ScreenRows), 1))
end
#endif


/*
string c_stPrefix[] = "PROJ_Prefix"
string proc GetPrefix()
	string st[32] = GetGlobalStr(c_stPrefix)
	return iif(Length(st), st, "File:")
end
*/


#ifdef NEED_GRAYIZE
integer proc Grayize(integer attr)
	integer target = Color(white on black)

	if attr & 0x0f == Color(black on black) and
			attr & 0xf0 <> Color(blink black on black)
		target = Color(bright black on black)
	endif

	if (attr & 0xf0) shr 4 == target
		// background is same as target, use bright black
		return((attr & 0xf0) | Color(bright black on black))
	endif

	return((attr & 0xf0) | (target & 0x0f))
end
#endif


string proc GetFullProjDir()
	return(AddTrailingSlash(LoadDir() + Get_Str(iProjDir)))
end


#ifdef NEED_GETOPENPATH
string proc GetOpenPath()
	string st[MAXPATH] = GetGlobalStr(c_stOpenPath)
	ExpandEnvVars(st)
	return(st)
end
#endif


string proc RemoveQuotes(string st)
	return(iif(Length(st) and st[1] == '"', st[2:Length(st)-2], st))
end


string proc NextToken(var string tok)
	string st[32] = ""

	tok = LTrim(tok)
	st = GetToken(tok, " ", 1)
	tok = DelStr(tok, 1, Length(st))
	return(st)
end


integer proc ValNextToken(var string tok)
	return (Val(NextToken(tok)))
end


#ifdef NEED_SETXY
proc SetXY(integer x, integer y)
	Set(X1, x)
	if y + NumLines() < Query(ScreenRows) - iif(Query(ShowHelpLine), 1, 0)
		Set(Y1, y)
	elseif NumLines() + 2 >= Query(ScreenRows) - iif(Query(ShowHelpLine), 1, 0)
		Set(Y1, 1)
	else
		Set(Y1, (Query(ScreenRows) - iif(Query(ShowHelpLine), 1, 0)) - (NumLines() + 1))
	endif
end
#endif


/*
integer proc BufferChanged(integer id)
	integer cid
	integer fChanged

	// it might look like Push/PopPosition() would be more efficient, but they
	// would cause hooks to fire when we don't want them to!
	cid = GotoBufferId(id)
	fChanged = FileChanged()
	GotoBufferId(cid)
	return(fChanged)
end
*/


#ifdef NEED_UPDATEHISTORYSTR
proc UpdateHistoryStr(string s, integer id)
	if FindHistoryStr(s, id)
		DelHistoryStr(id, FindHistoryStr(s, id))
	endif
	AddHistoryStr(s, id)
end
#endif


#ifdef NEED_WRITEPROFILE
proc WriteProfile()
	if Get_Int(iFOpenLast)
		WriteProfileStr(c_stIniSection, c_stIniLastProject, Get_Str(iProjectFilename))
	endif
	FlushProfile()
end
#endif


#ifdef NEED_INIVERSION
integer proc GetIniVersion()
	return(Val(GetProfileStr(c_stIniSection, c_stIniVersion, "0"), 16))
end
#endif


#ifdef NEED_GRAYCHAINCMD
proc GreyChainCmd()
	if Query(Key) & GREY_KEY
		PushKey(Query(Key) & (~GREY_KEY))
	else
		ChainCmd()
	endif
end
#endif


integer proc CDD(string dir)
	integer i
	string s[4]

	if not Length(dir)
		return (FALSE)
	endif

	s = SplitPath(dir, _DRIVE_)
	if Length(s) <> 0
		if s[1] == "\"
			// can't CD to a UNC path
			return (FALSE)
		endif
		i = ChDir(s)
		if s == dir
			return (i)
		endif
	endif
	i = iif(dir[Length(dir)] == "\", 1, 0)
	return (ChDir(SubStr(dir, 1, Length(dir) - i)))
end


#ifdef NEED_SCANFOR
string proc ScanFor(string _stDir, string stScan, string stDefault)
	string stDir[MAXPATH] = _stDir
	string stDrive[MAXPATH]
	string stFile[MAXPATH] = stDefault

	Set(Break, ON)

	if Length(stScan)
		if not Length(stDir)
			stDir = CurrDir()
		endif

		loop
			if FileExists(AddTrailingSlash(stDir) + stScan)
				stFile = AddTrailingSlash(stDir) + stScan
				break
			endif

			stDrive = SplitPath(stDir, _DRIVE_)
			stDir = SplitPath(stDir, _PATH_)
			if not Length(stDir) or stDir == "\"
				break
			endif

			stDir = RemoveTrailingSlash(stDrive + stDir)
		endloop
	endif

	Set(Break, OFF)

	return(stFile)
end
#endif


#ifdef NEED_MSEARCHPATH
// ExpandEnvVars()
// return string with environment variables expanded.  (use %@ to reference
// TSE global vars).
integer proc ExpandEnvVars(var string st)
	integer i, j
	string env[33]
	string s[MAXPATH]
	string stValue[MAXPATH]
	string stNotFound[MAXPATH] = ""

	s = st
	st = ""
	for i = 1 to Length(s)
		if s[i:2] == "%%"
			i = i + 1								// inc extra time
			st = st + "%"							// append %
		elseif s[i] == "%"
			i = i + 1								// inc past %
			j = i
			while i <= Length(s) and (s[i] in 'A'..'Z', 'a'..'z', '0'..'9', '_', '@')
				i = i + 1
			endwhile
			env = s[j:(i - j)]
			if s[i] <> "%"
				i = i - 1
			endif
			if Length(env)
				stValue = iif(env[1] == "@",
							  GetGlobalStr(env[2:sizeof(env)]),
							  GetEnvStr(env))
				if Length(stValue)
					st = st + stValue
				else
					stNotFound = iif(Length(stNotFound), stNotFound+", ", stNotFound) + env
				endif
			endif
		else
			st = st + s[i]							// copy char
		endif
	endfor

	#ifdef DEBUG
	Warn("Expanded: [" + st + "] from [" + s + "]")
	#endif

	if Length(stNotFound)
		//TimeOutMessage(ERROR_TIMEOUT, "One or more environment variables are empty.")
		TimeOutMessage(ERROR_TIMEOUT, "Environment variables empty: "+stNotFound+".")
		//Warn("Environment variables empty:"; stNotFound, ".")
	endif

	return(st <> s)
end
#endif


#ifdef NEED_MCREATETEMPBUFFER
integer proc mCreateTempBuffer()
	integer cid = GetBufferId()
	integer idTmp

	idTmp = CreateTempBuffer()
	GotoBufferId(cid)
	return (idTmp)
end
#endif


#ifdef NEED_MSEARCHPATH
// mSearchPath()
// behaves like SearchPath, but can deal with path string up to TSE maximum
// string length.  also expands environment variables.  a given path cannot
// exceed the TSE maximum string length, either.  environment variables are
// expanded before evaluating each path instead of once at the beginning.
string proc mSearchPath(string fn, string path, string sub)
	integer ich = 1, j = 1
	integer iSemi
	string s[MAXPATH] = ""
	string tmp[MAXPATH] = ""

	if Length(path)
		while Length(s) == 0 and ich <= Length(path)
			//$ todo: doesn't deal with ";" inside quotes
			//$ todo: doesn't deal with quotes, period
			j = Pos(";", SubStr(path, ich, Length(path)))
			if j == 0
				iSemi = (Length(path) - ich) + 2
			else
				iSemi = j
			endif

			tmp = SubStr(path, ich, iSemi - 1)
			ExpandEnvVars(tmp)
			s = ExpandPath(tmp + "\" + fn)
			if Length(s) == 0 and Length(sub) > 0
				s = ExpandPath(tmp + "\" + sub + "\" + fn)
			endif
			if not FileExists(s)
				s = ""
			endif

			ich = ich + iSemi
		endwhile
	else
		if Length(sub)
			s = SearchPath(fn, path, sub)
		else
			s = SearchPath(fn, path)
		endif
	endif

	return(s)
end
#endif



// Pretty Filenames -------------------------------------------------------

string proc CapToken(string st)
	string stCap[255] = st
//	integer fLower = FALSE
//	integer fUpper = FALSE
	integer fSpace = FALSE
	integer i

	for i = Length(st) downto 1
//		fLower = fLower or (Asc(st[i]) in Asc("a")..Asc("z"))
//		fUpper = fUpper or (Asc(st[i]) in Asc("A")..Asc("Z"))
//		if fLower and fUpper
//			break
//		endif
		if Asc(st[i]) == 32		//space
			fSpace = TRUE
			break
		endif
	endfor

	if not fSpace //and fUpper <> fLower
		stCap = Lower(stCap)
		for i = 1 to Length(stCap)
			if Asc(stCap[i]) in Asc("a")..Asc("z")
				stCap[i] = Upper(stCap[i])
				break
			endif
		endfor
	endif

	return (stCap)
end


/*
string proc CapPath(string st, string stDelim)
	string stCap[255] = ""
	//string stToken[255]
	integer cTokens
	integer i
	//integer fDot = (stDelim == ".")

	// the rest is easier if cTokens = number of slashes + 1
	//cTokens = NumTokens(st, "\/")
	cTokens = 1
	for i = Length(st) downto 1
		if Pos(st[i], stDelim)
			cTokens = cTokens + 1
		endif
	endfor

	for i = 1 to cTokens
		if i > 1
			stCap = stCap + stDelim[1]
		endif
		//stToken = GetToken(st, stDelim, i)
		//if not fDot and Pos(".", stToken) and not Pos(" ", stToken)
		//	  stCap = stCap + CapPath(stToken, ".")
		//else
			stCap = stCap + CapToken(GetToken(st, stDelim, i))
		//endif
	endfor

	return (stCap)
end
*/


/*
string proc PrettyFilename(string fn)
	string stPretty[255] = ""

//	string st[255]
//
//	st = SplitPath(fn, _DRIVE_)
//	if Length(st) == 2 and st[2] == ":"
//		stPretty = stPretty + Lower(st)
//	else
//		stPretty = stPretty + CapPath(st)
//	endif
//	stPretty = stPretty + CapPath(SplitPath(fn, _PATH_|_EXT_))

//	stPretty = Lower(SplitPath(fn, _DRIVE_|_PATH_))
//	//stPretty = stPretty + CapPath(SplitPath(fn, _NAME_|_EXT_), "\/")
//	stPretty = stPretty + CapToken(SplitPath(fn, _NAME_|_EXT_))

	stPretty = CapToken(SplitPath(fn, _NAME_|_EXT_)) + " (" +
			Lower(SplitPath(fn, _DRIVE_|_PATH_)) + ")"

	return (stPretty)
end
*/



// Window -----------------------------------------------------------------

proc DeferredRefresh()
	UnHook(DeferredRefresh)
	UpdateDisplay(_ALL_WINDOWS_REFRESH_)
end

proc RestoreWindow(var integer id)
	integer cid
	integer x1, y1, cols, rows
	integer i, j, k, l
	string s[255], a[255]

	if not id
		return()
	endif

	cid = GotoBufferId(id)
	if cid
		BegFile()
		s = GetText(1, 255)
		x1 = ValNextToken(s)
		y1 = ValNextToken(s)
		cols = ValNextToken(s)
		rows = ValNextToken(s)
		KillLine()
		EndFile()

		i = Query(PopWinX1)
		j = Query(PopWinY1)
		k = Query(PopWinCols)
		l = Query(PopWinRows)
		Window(1, 1, Query(ScreenCols), Query(ScreenRows))

		while rows
			rows = rows - 1
			a = GetText(1, cols)
			Up()
			s = GetText(1, cols)
			Up()
			VGotoXYAbs(x1, y1+rows)
			PutStrAttr(s, a)
		endwhile

		Window(i, j, i+k-1, j+l-1)

		GotoBufferId(cid)
		AbandonFile(id)
	endif
	id = 0

	Hook(_AFTER_COMMAND_, DeferredRefresh)
end


integer proc SaveWindow(integer x1, integer y1, integer cols, integer rows)
	integer cid = GetBufferId()
	integer id
	integer i
	string s[255] = ""
	string a[255] = ""

	id = CreateTempBuffer()
	if id
		BegFile()
		for i = 1 to rows
			VGotoXYAbs(x1, y1+i-1)
			if GetStrAttr(s, a, cols)
				AddLine(s)
				AddLine(a)
			else
				// error: clean up and abort
				GotoBufferId(cid)
				AbandonFile(id)
				id = 0
				goto error_saving_window
			endif
		endfor
		BegFile()
		InsertLine(Format(x1; y1; cols; rows))
		GotoBufferId(cid)
	else
		error_saving_window:
		Warn("error saving window")
	endif

	return(id)
end


integer proc PushMessage(string msg)
	integer id

	id = SaveWindow(1, Query(StatusLineRow), Query(ScreenCols), 1)
	Message(msg)
	return (id)
end


proc PopMessage(var integer id, integer fRestore)
	if fRestore
		RestoreWindow(id)
	else
		AbandonFile(id)
		id = 0
	endif
end



// Misc -------------------------------------------------------------------

#if 0
// RegExStr()
// returns string with regular expression operator chars escaped so it can be
// used as part of a regex string.
string proc RegExStr(string st)
	integer i, j
	integer n = Length(st)
	string stRet[255] = ""

	j = 1
	for i = 1 to n
		// escape regex chars
		if Pos(st[i], "[]{}\.*+@#^$|?")
			if j + 1 >= sizeof(stRet)
				// avoid escape char being last char in string
				break
			endif
			stRet[j] = '\'
			j = j + 1
		endif
		stRet[j] = st[i]
		j = j + 1
	endfor
	return(stRet)
end
#endif



// Version ----------------------------------------------------------------

proc ShowVersion(integer fTimeout)
	integer attr
	integer y
	integer i, j, k, l

	y = Query(StatusLineRow)

	i = Query(PopWinX1)
	j = Query(PopWinY1)
	k = Query(PopWinCols)
	l = Query(PopWinRows)
	Window(1, y, Query(ScreenCols), y)

	attr = Set(MsgAttr, Color(bright white on black))
	if fTimeout
		TimeOutMessage(3, "")
	else
		Message("")
	endif
	Set(MsgAttr, attr)

	VGotoXY(1, 1)
	mPutOemStr("ù ", Color(bright red on black), FALSE)
	mPutOemStr("PROJ", Color(bright yellow on black), FALSE)
	mPutOemStr("/", Color(bright white on black), FALSE)
	mPutOemStr(c_stVer, Color(bright green on black), FALSE)
	mPutOemStr(" Û²±°", Color(blue on black), FALSE)

	VGotoXY(24, 1)
	if Get_Int(iProjectId)
		PutStr("Project: ", Color(bright white on black))
		//PutStr(SqueezePath(g_fnProject, Query(ScreenCols) - (32+10+22)), Color(bright yellow on black))
		PutStr(Get_Str(iProjectTitle), Color(bright yellow on black))
	else
		PutStr("No project open.", Color(bright white on black))
	endif

	//PutStrXY(Query(ScreenCols)-22+1, 1, "chrisant@microsoft.com", Color(bright white on black))
	PutStrXY(Query(ScreenCols)-Length(CurrDir())+1, 1, CurrDir(), Color(bright white on black))

	Window(i, j, i+k-1, j+l-1)
end



///////////////////////////////////////////////////////////////////////////
// Command Line Arguments

#ifdef NEED_DOSCMDLINEOPTIONUSED
integer proc QuotedArg(string quote, var string s, var string arg)
	integer i
	integer j

	arg = ""
	// is arg quoted with <quote>?
	i = Pos(s[1], quote)
	if i
		// remove beg-quote from temp
		s = DelStr(s, 1, 1)
		j = Pos(quote[i], s)
		if j
			// get quoted arg
			arg = s[1:j-1]
			if j == Length(s) or (j < Length(s) and s[j+1] == " ")
				// remove arg, end-quote, and space from temp
				s = DelStr(s, 1, Length(arg)+2)
				return(TRUE)
			else
				Warn("Quoted argument improperly formed.")
			endif
		else
			Warn("Argument missing end-quote.")
		endif
	endif
	return(FALSE)
end


// DosCmdLineOptionUsed()
// looks for -<option>, sets global str Arg<option> if found
integer proc DosCmdLineOptionUsed(STRING option)
	string tmp[255]
	string stCmdline[255] = ""
	string arg[80] = ""
	string opt[20] = "-" + option
	integer fFound = FALSE
	integer ii

	for ii = 1 to NumFileTokens(Query(DosCmdLine))
		// get token
		tmp = GetFileToken(Query(DosCmdLine), ii)
		if not fFound
			if Pos(opt, tmp) == 1
				// option found
				if Length(opt) == Length(tmp)
					// no argument used
					arg = ""
				else
					// argument used
					tmp = DelStr(tmp, 1, Length(opt))
					if not QuotedArg(Chr(1)+Chr(34)+Chr(39), tmp, arg)
						arg = tmp
					endif
				endif
				// remove this from the cmd line
				tmp = ""
				// set arg variable
				SetGlobalStr('Arg' + option, arg)
				fFound = TRUE
			endif
		endif

		// build the updated cmd line
		if Length(tmp)
			if Length(stCmdline)
				stCmdline = stCmdline + " "
			endif
			stCmdline = stCmdline + tmp
		endif
	endfor

	// set the updated cmd line
	Set(DosCmdLine, stCmdline)

	return(fFound)
end
#endif



// Project File Manipulation ----------------------------------------------

proc UpToLastBlankLine()
	repeat
		Up()
	until CurrLineLen()
	Down()
end


// FindProjectSection()
// find project section, append it to end of file if it doesn't exist yet.
proc FindProjectSection(string st)
	if not lFind(st, "^$gi")
		EndFile()
		if CurrLineLen()
			AddLine()
		endif
		AddLine()
		InsertLine(st)
	endif

	if not Down()
		AddLine()
	endif
end


// MarkProjectSection()
// mark the lines in a project section.
integer proc MarkProjectSection(string st)
	integer lnStart

	// find beginning of section
	FindProjectSection(st)

	// mark to end of section
	UnMarkBlock()
	lnStart = CurrLine()
	if not lFind("\[.*\]", "^$xi")
		EndFile()
		// last line must be blank!	 we'll fix it if not, but we'll warn also
		if CurrLineLen()
			Addline()
			Warn("last line must be blank!")
		endif
	endif
	UpToLastBlankLine()
	if CurrLine() > lnStart
		MarkLine(lnStart, CurrLine() - 1)
	endif

	// place cursor at top of block
	if isBlockMarked()
		GotoBlockBegin()
		BegLine()
	else
		// no block, and cursor is on a blank line immediately following the
		// section name.
	endif

	return (isBlockMarked())
end


// FindProjectVar()
// find project var
integer proc FindProjectVar(string stItem)
	return (isBlockInCurrFile() and lFind(stItem+"=\c", "^glxi"))
end


// GetProjectStr()
// like GetProfileStr, but works on a marked block; you must first mark the
// block you want it to search in (eg, via MarkProjectSection).
string proc GetProjectStr(string stItem, string stDefault)
	if FindProjectVar(stItem)
		return (GetText(CurrPos(), 255))
	endif
	return (stDefault)
end


#ifdef NEED_GETPROJECTINT
// GetProjectInt()
// like GetProfileInt, but works on a marked block; you must first mark the
// block you want it to search in (eg, via MarkProjectSection).
integer proc GetProjectInt(string stItem, integer nDefault)
	if FindProjectVar(stItem)
		return (Val(GetText(CurrPos(), 255)))
	endif
	return (nDefault)
end
#endif


// SetProjectStr()
// like SetProfileStr, but works on a marked block; you must first mark the
// block you want it to search in (eg, via MarkProjectSection).
proc SetProjectStr(string stItem, string stVal)
	if FindProjectVar(stItem)
		// found; replace it if different
		if stVal <> GetText(CurrPos(), 255)
			KillToEol()
			InsertText(stVal)
		endif
	else
		// not found; add it
		if isBlockMarked()
			GotoBlockEnd()
			AddLine(stItem+"="+stVal)
			MarkLine()
		else
			// cursor is on a blank line immediately after the section name
			#ifdef DEBUG
			Assert(not CurrLineLen(), "hey, this should be a blank line!")
			#endif
			Up()
			AddLine(stItem+"="+stVal)
			MarkLine(CurrLine(), CurrLine())
		endif
	endif
end


#ifdef NEED_SETPROJECTINT
// SetProjectInt()
// like SetProfileInt, but works on a marked block; you must first mark the
// block you want it to search in (eg, via MarkProjectSection).
proc SetProjectInt(string stItem, integer nVal)
	SetProjectStr(stItem, Str(nVal))
end
#endif


#ifdef NEED_CALCPROJECTROOT
string proc CalcProjectRoot(string _fn)
	string fn[_MAXPATH_] = RemoveQuotes(_fn)

	if Lower(SplitPath(fn, _NAME_|_EXT_)) == Lower(c_stLocalProject)
		return(RemoveTrailingSlash(SplitPath(fn, _DRIVE_|_PATH_)))
	endif

	return("")
end
#endif


#ifdef NEED_SETPROJECTROOT
proc SetProjectRoot(string fn)
	string stRoot[_MAXPATH_] = CalcProjectRoot(fn)

	if Length(stRoot)
		SetGlobalStr(c_stProjectRoot, stRoot)
	else
		DelGlobalVar(c_stProjectRoot)
	endif
end
#endif


// GetProjectFullPathname()
// returns fully qualified pathname of fnProject
string proc GetProjectFullPathname(string fnProject)
	string fn[MAXPATH] = fnProject

	// if no dir specified, default to GetFullProjDir
	if SplitPath(fn, _DRIVE_|_PATH_) == ""
		fn = GetFullProjDir()+fn
	endif

	// always force .pj extension!
	if SplitPath(fn, _EXT_) <> c_stExt
		fn = fn + c_stExt
	endif
	return(fn)
end



// AutoLoad section -------------------------------------------------------

#ifdef NEED_INSERTAUTOLOAD
integer proc InsertAutoLoad(string stPath, string _stProject)
	string stProject[_MAXPATH_] = _stProject

	if Length(stProject)
		stProject = RemoveQuotes(stProject)
		if Lower(SplitPath(stProject, _DRIVE_|_PATH_)) == Lower(GetFullProjDir())
			// if in default dir for project files, strip path
			stProject = SplitPath(stProject, _NAME_|_EXT_)
		endif

		// strip extension
		stProject = SplitPath(stProject, _DRIVE_|_NAME_)

		// add path and project to autoload section
		#ifdef DEBUG
		PushPosition()
		List("debugging InsertAutoLoad", 78)
		PopPosition()
		#endif
		InsertLine(stPath + c_stDiv + stProject)
		return (TRUE)
	endif
	return (FALSE)
end
#endif


