// MASTER FILE: ["proj.si"]
//#pragma inhouse,chrisant

// TIMEOUT.SI -------------------------------------------------------------



///////////////////////////////////////////////////////////////////////////
// TimeOutMessage

integer idle_timeout
constant ONE_SECOND = 18


forward proc BeforeCommand_TimeOutMessage()


proc Idle_TimeOutMessage()
	if GetClockTicks() > idle_timeout
		BeforeCommand_TimeOutMessage()
		UpdateDisplay(_STATUSLINE_REFRESH_)
	endif
end


proc BeforeCommand_TimeOutMessage()
	Set_Int(iFTimeoutHooked, FALSE)
	UnHook(BeforeCommand_TimeOutMessage)
	UnHook(Idle_TimeOutMessage)
end


proc TimeOutMessage(integer cSeconds, string msg)
	integer attr = Query(MsgAttr)

	BeforeCommand_TimeOutMessage()
	Set_Int(iFTimeoutHooked, TRUE)
	idle_timeout = GetClockTicks() + cSeconds*ONE_SECOND
	if cSeconds == ERROR_TIMEOUT
		Set(MsgAttr, Color(bright white on red))
	endif
	Message(msg)
	Set(MsgAttr, attr)
	Hook(_AFTER_UPDATE_STATUSLINE_, BeforeCommand_TimeOutMessage)
	Hook(_BEFORE_COMMAND_, BeforeCommand_TimeOutMessage)
	Hook(_IDLE_, Idle_TimeOutMessage)
end


